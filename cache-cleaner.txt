-- get Username of current User
set userName to do shell script "whoami"

-- create Filepath with Username
set folderPath to "Macintosh HD:Users:" & userName & ":Movies"

-- define Filetype
set fileType to "fcpcache"

-- check if Cache-Files Exist
tell application "Finder"
	set cacheFiles to (every file of folder folderPath whose name extension is fileType)
	set cacheCount to count of cacheFiles
	set totalSize to 0
	repeat with aFile in cacheFiles
		set totalSize to totalSize + (size of aFile)
	end repeat
end tell

-- choose correct Unit to Display Filesize
set bestUnit to "B"
if totalSize > 1024 then set bestUnit to "KB"
if totalSize > 1024 * 1024 then set bestUnit to "MB"
if totalSize > 1024 * 1024 * 1024 then set bestUnit to "GB"
if totalSize > 1024 * 1024 * 1024 * 1024 then set bestUnit to "TB"

-- calculate correct units
if bestUnit = "KB" then set totalSize to totalSize / (1024)
if bestUnit = "MB" then set totalSize to totalSize / (1024 * 1024)
if bestUnit = "GB" then set totalSize to totalSize / (1024 * 1024 * 1024)
if bestUnit = "TB" then set totalSize to totalSize / (1024 * 1024 * 1024 * 1024)

-- round calculated unit
set roundedTotalSize to (round (totalSize * 100)) / 100

-- act based on Cache-Files
if cacheCount > 0 then
	set pluralSingularText to "Cache-File"
	if cacheCount > 1 then set pluralSingularText to "Cache-Files"
	
	set dialogText to "There exist/s " & (cacheCount as text) & " " & pluralSingularText & " with a total Size of " & (roundedTotalSize as text) & " " & bestUnit & ".
Do you want to move it to Trash?"
	set dialogResult to display alert dialogText buttons {"Cancel", "Yes"} default button "Yes" cancel button "Cancel"
	
	if button returned of dialogResult is "Yes" then
		-- Delete Cache-Files
		tell application "Finder"
			delete cacheFiles
		end tell
		
		-- Empty Trash
		set emptyTrashResult to display alert "Do you want to empty Trash?" buttons {"No", "Yes"} default button "Yes" cancel button "No"
		if button returned of emptyTrashResult is "Yes" then
			tell application "Finder"
				empty trash
			end tell
		end if
	end if
else
	display alert "No Cache-Files found"
end if
